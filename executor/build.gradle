apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

mainClassName = "org.apache.mesos.elasticsearch.executor.Main"
ext {
    imageName = imagePrefix + '/elasticsearch-executor'
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
    }
}

repositories {
    maven {
        url "https://jitpack.io"
    }
}

dependencies {

    compile project(':commons')

    compile "org.elasticsearch:elasticsearch:${elasticsearchVersion}"
    compile "log4j:log4j:1.2.16"
    compile "org.apache.zookeeper:zookeeper:3.4.6"
    compile "com.github.containersolutions:elasticsearch-zookeeper:v${elasticsearchVersion}"
    compile "com.beust:jcommander:1.48"
}

shadowJar {
    baseName = "elasticsearch-mesos-executor"
    mergeServiceFiles()
    classifier = ''
}

publishing {
  publications {
    shadow(MavenPublication) {
      from components.shadow
    }
  }
}

task taskCopyFilesForDocker(type: Copy) {
    dependsOn "copyShellScript", "copyJar"
}

task copyJar(type: Copy) {
    dependsOn 'shadowJar'
    from { getTasksByName("shadowJar", false)[0].outputs.files[0] } // Get outputs from task "jar"
    into        'build/docker'
    rename { String fileName ->
        fileName.replace("-${project.version}", "")
    }
}

task copyShellScript(type: Copy) {
    from        "start-executor.sh"
    into        'build/docker'
}
